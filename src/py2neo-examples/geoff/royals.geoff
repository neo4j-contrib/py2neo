# This data represents a portion of the British Royal Family

# First, we define the nodes (in this case, people)
(Liz)	{"name": "Elizabeth", "title": "Queen of the Commonwealth Realms", "birth.date": "1926-04-21"}
(Phil)	{"name": "Philip", "title": "Duke of Edinburgh", "birth.date": "1921-06-21"}
(Chaz)	{"name": "Charles", "title": "Prince of Wales", "birth.date": "1948-11-14"}
(Di)	{"name": "Diana", "title": "Princess of Wales", "birth.date": "1961-07-01", "death.date": "1997-08-31"}
(Cam)	{"name": "Camilla", "title": "Duchess of Cornwall", "birth.date": "1947-07-17"}
(Wills)	{"name": "William", "title": "Duke of Cambridge", "birth.date": "1982-06-21"}
(Harry)	{"name": "Henry", "birth.date": "1984-09-15"}
(Kate)	{"name": "Catherine", "title": "Duchess of Cambridge", "birth.date": "1982-01-09"}

# Next come the relationships between the people
(Liz)-[1947:MARRIED]->(Phil)    {"marriage.place": "Westminster Abbey", "marriage.date": "1947-11-20"}
(Liz)-[:MOTHER_OF]->(Chaz)
(Phil)-[:FATHER_OF]->(Chaz)
(Di)-[1981:MARRIED]->(Chaz)     {"marriage.place": "St Paul's Cathedral", "marriage.date": "1981-07-29", "divorce.date": "1996-08-28"}
(Di)-[:MOTHER_OF]->(Wills)
(Chaz)-[:FATHER_OF]->(Wills)
(Di)-[:MOTHER_OF]->(Harry)
(Cam)-[2005:MARRIED]->(Chaz)    {"marriage.place": "Windsor Guildhall", "marriage.date": "2005-04-09"}
(Kate)-[2011:MARRIED]->(Wills)  {"marriage.place": "Westminster Abbey", "marriage.date": "2011-04-29"}

# And finally the index entries for the nodes and relationships
|People|->(Liz)     {"name": "Elizabeth"}
|People|->(Phil)    {"name": "Philip"}
|People|->(Chaz)    {"name": "Charles"}
|People|->(Di)      {"name": "Diana"}
|People|->(Cam)     {"name": "Camilla"}
|People|->(Wills)   {"name": "William"}
|People|->(Harry)   {"name": "Henry"}
|People|->(Kate)    {"name": "Catherine"}
|Weddings|->[1947]  {"year": 1947}
|Weddings|->[1981]  {"year": 1981}
|Weddings|->[2005]  {"year": 2005}
|Weddings|->[2011]  {"year": 2011}

# Hook into the supplied node
{0}-[:FAMILY]->(Liz)

